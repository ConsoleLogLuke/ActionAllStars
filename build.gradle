buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.ConsoleLogLuke:GradleFx:-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:4.1.2'
    }
}

subprojects {
    apply plugin: 'gradlefx'
    apply plugin: 'scaffold'
    apply plugin: 'de.undercouch.download'

    group 'com.sdg'
    version '1.0.0'

    ext {
        flexSdkName = 'flex4.6'
        flexSdkVersion = '4.6.0.23201B'
    }

    dependencies {
        flexSDK rootProject.files("sdks/flex_sdk_${flexSdkVersion}.zip")
    }


    additionalCompilerOptions = ['-warnings=false']

    afterEvaluate {
        def sdkDir = rootProject.file('sdks')
        def sdkZipName = "flex_sdk_${flexSdkVersion}.zip"
        def sdkZip = new File(sdkDir, sdkZipName)
        if (sdkZip.exists()) {
            return
        }
        if (!sdkDir.exists()) {
            sdkDir.mkdirs()
        }

        download {
            src "http://download.macromedia.com/pub/flex/sdk/builds/$flexSdkName/$sdkZipName"
            dest sdkDir
            overwrite false
        }
    }

    compileFlex.doFirst {
        def haloSwc = "$flexHome/frameworks/themes/Halo/halo.swc"
        additionalCompilerOptions += "-theme=$haloSwc"
    }
}

tasks.register('cleanLocalSdks', Delete) {
    delete rootProject.file('sdks')
}
