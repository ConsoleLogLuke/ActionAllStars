buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.ConsoleLogLuke:GradleFx:-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:4.1.2'
    }
}

subprojects {
    apply plugin: 'gradlefx'
    apply plugin: 'scaffold'
    apply plugin: 'de.undercouch.download'

    group 'com.sdg'
    version '1.0-SNAPSHOT'

    ext {
        flexSdkName = 'flex3'
        flexSdkVersion = '3.6.0.16995A'
        flexSdkFileName = flexSdkFileNamePattern
                .replace('[name]', flexSdkName)
                .replace('[version]', flexSdkVersion)
        flexSdkUrl = flexSdkUrlPattern
                .replace('[name]', flexSdkName)
                .replace('[version]', flexSdkVersion)
                .replace('[fileName]', flexSdkFileName)
    }

    dependencies {
        flexSDK rootProject.files("sdks/${flexSdkFileName}")
    }

    afterEvaluate {
        def sdkDir = rootProject.file('sdks')
        def sdkZip = new File(sdkDir, flexSdkFileName)
        if (sdkZip.exists()) {
            return
        }
        if (!sdkDir.exists()) {
            sdkDir.mkdirs()
        }

        download {
            src flexSdkUrl
            dest sdkDir
            overwrite false
        }
    }
}

tasks.register('cleanLocalSdks', Delete) {
    def sdkDir = rootProject.file('sdks')
    delete sdkDir
}
